subprojects {
    apply plugin: 'java'
    apply plugin: 'osgi'
    apply plugin: 'eclipse'
    
    version = '0.6'
    group = 'de.undercouch'
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
    
    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/public'
        }
    }
    
    dependencies {
        testCompile 'junit:junit:4.11'
    }
    
    //configure javadoc
    javadoc {
        //exclude internal packages
        exclude '**/internal/**'
        
        //link to JRE6 documentation
        options.author true
        options.links 'http://download.oracle.com/javase/6/docs/api/'
    }
    
    // package javadoc into a jar file
    task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
        from javadoc.destinationDir
        classifier = 'javadoc'
    }
    
    // package source into a jar file
    task packageSources(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }
}

// initialize gradle wrapper
task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
