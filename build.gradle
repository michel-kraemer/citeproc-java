plugins {
    id 'io.github.gradle-nexus.publish-plugin' version "2.0.0"
}

group = 'de.undercouch'
version = '3.4.0'

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'java'

    group = rootProject.group
    version = rootProject.version

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/public'
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.13.2'
    }
    
    // configure javadoc
    javadoc {
        // exclude internal packages
        exclude '**/internal/**'
        
        // link to Java 11 documentation
        options.author true
        options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        options.source '11' // Java version
    }

    // package javadoc and sources
    java {
        withJavadocJar()
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true
        }
    }
}
